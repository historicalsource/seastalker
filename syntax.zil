"SYNTAX for SEASTALKER
Copyright (C) 1984 Infocom, Inc.  All rights reserved."

"SUBTITLE VOCABULARY"

<BUZZ	A AN THE AND OF THEN ONE ;BUT ;EXCEPT \. \, \" \! \? ;Y BACK
	;CARRYING ;HOLDING FIRST NOW SOON WELL THERE ;YOUR ;YOU PLEASE MAYBE
	CAREFULLY CLOSELY ;THOROUGHLY QUIETLY SLOWLY QUICKLY BRIEFLY PRIVATELY
	ALL AGAIN G>

<ROUTINE CAPITAL-NOUN? (WRD)
	<OR <EQUAL? .WRD ,W?DOC ,W?TIP ,W?RANDALL>
	    <EQUAL? .WRD ,W?DOC\'S ,W?TIP\'S ,W?BILL\'S>
	    <EQUAL? .WRD ,W?KEMP\'S ,W?ZOE\'S ,W?MARV\'S>
	    <EQUAL? .WRD ,W?SHARON ,W?KEMP ,W?ZOE>
	    <EQUAL? .WRD ,W?BLY\'S ,W?AMY\'S ,W?FROBTON>
	    <EQUAL? .WRD ,W?BLY ,W?AMY ,W?LOWELL>
	    <EQUAL? .WRD ,W?MICK\'S ,W?WALT\'S>
	    <EQUAL? .WRD ,W?MICK ,W?ANTRIM ,W?WALT>
	    <EQUAL? .WRD ,W?DOCTOR ,W?DR ,W?MARV>
	    <EQUAL? .WRD ,W?SIEGEL ,W?BILL ,W?GREENUP>
	    <EQUAL? .WRD ,W?HORVAK ,W?JEROME ,W?THORPE>>>

<ROUTINE BUZZER-WORD? (WORD)
 <COND (<QUESTION-WORD? .WORD> <RTRUE>)
       (  <NUMBER-WORD? .WORD> <RTRUE>)
       ( <NAUGHTY-WORD? .WORD> <RTRUE>)
       (<EQUAL? .WORD ,W?ALL ,W?AGAIN ,W?G>
	<TELL "(Sorry, but in this story you can't use the word \"">
	<PRINTB .WORD>
	<TELL "\".)" CR>)>>

<BUZZ	AM ANY ARE CAN COULD DID DO HAS HAVE HE\'S HOW
	IS IT\'S I\'LL I\'M I\'VE LET\'S SHALL SHE\'S SHOULD
	THAT\'S THEY\'RE WAS WERE WE\'RE
	WHAT WHAT\'S WHEN WHEN\'S WHERE ;WHERE\'S WHICH WHO WHO\'S WHY
	WILL WON\'T WOULD YOU\'RE>

<GLOBAL QUESTION-WORD-COUNT 0>
<ROUTINE QUESTION-WORD? (WORD)
	<COND (<EQUAL? .WORD ,W?WHERE ;",W?THERE ,W?SEEN">
	       <TELL
"(To locate something, use the command: FIND " D ,SOMETHING ".)" CR>)
	      (<OR <EQUAL? .WORD ,W?WHAT ,W?WHAT\'S>
		   <EQUAL? .WORD ,W?WHO ,W?WHO\'S>>
	       <TELL
"(To ask about something, use the command: TELL ME ABOUT " D ,SOMETHING ".)"
CR>)
	      (<OR <EQUAL? .WORD ,W?THAT\'S	,W?IT\'S>
		   <EQUAL? .WORD ,W?WHY		,W?HOW		,W?WHEN>
		   <EQUAL? .WORD ,W?IS		,W?DID		,W?ARE>
		   <EQUAL? .WORD ,W?DO		,W?HAVE		,W?ANY>
		   <EQUAL? .WORD ,W?AM		,W?I\'M		,W?WE\'RE>
		   <EQUAL? .WORD ,W?WILL	,W?WAS		,W?WERE>
		   <EQUAL? .WORD ,W?I\'LL	,W?CAN		,W?WHICH>
		   <EQUAL? .WORD ,W?I\'VE	,W?WON\'T	,W?HAS>
		   <EQUAL? .WORD ,W?YOU\'RE	,W?HE\'S	,W?SHE\'S>
		   <EQUAL? .WORD ,W?SHOULD	,W?WOULD	,W?WHEN\'S>
		   <EQUAL? .WORD ,W?THEY\'RE	,W?COULD	,W?SHALL>>
	       <TELL "(Please use commands">
	       <INC QUESTION-WORD-COUNT>
	       <COND (<G? ,QUESTION-WORD-COUNT 9>
		      <SETG QUESTION-WORD-COUNT 0>
		      <TELL
"! Your commands tell the computer what you want to do in the story. You
can give commands to other people in the story, too. Here are examples
of commands:|
   TURN ON THE LAMP|
   LOOK UNDER THE RUG|
   SHARON, GIVE THE BOOK TO TIP|
   BILL, HELP ME|
Now you can try again.)" CR>)
		     (T <TELL ", not statements or questions.)" CR>)>
	       <RTRUE>)>>

<BUZZ	ZERO TWO THREE FOUR FIVE SIX SEVEN EIGHT NINE TEN ELEVEN TWELVE THIRTE
	FOURTE FIFTEE SIXTEE SEVENT EIGHTE NINETE TWENTY THIRTY FORTY ;FORTY-
	FIFTY ;FIFTY- SIXTY ;SIXTY- EIGHTY NINETY HUNDRE THOUSA MILLIO BILLIO>

<ROUTINE NUMBER-WORD? (WRD)
	<COND (<OR <EQUAL? .WRD ,W?ZERO>
		   <EQUAL? .WRD ,W?TWO ,W?THREE ,W?FOUR>
		   <EQUAL? .WRD ,W?FIVE ,W?SIX ,W?SEVEN>
		   <EQUAL? .WRD ,W?EIGHT ,W?NINE ,W?TEN>
		   <EQUAL? .WRD ,W?ELEVEN ,W?TWELVE ,W?THIRTE>
		   <EQUAL? .WRD ,W?FOURTE ,W?FIFTEE ,W?SIXTEE>
		   <EQUAL? .WRD ,W?SEVENT ,W?EIGHTE ,W?NINETE>
		   <EQUAL? .WRD ,W?TWENTY ,W?THIRTY ,W?FORTY>
		   <EQUAL? .WRD ,W?FIFTY ,W?SIXTY ,W?EIGHTY>
		   <EQUAL? .WRD ,W?NINETY ,W?HUNDRE ,W?THOUSA>
		   <EQUAL? .WRD ,W?MILLIO ,W?BILLIO ,W?ONE>>
	       <TELL "(Use numerals for numbers, for example \"10.\")" CR>
	       <RTRUE>)>>

<BUZZ	CURSE CURSES CUSS DAMN SHIT SUCK FUCK FUDGE PISS PEE POO SNOT BASTARD>

<ROUTINE NAUGHTY-WORD? (WORD)
 <COND (<OR <EQUAL? .WORD ,W?CURSE ,W?CURSES ,W?CUSS>
	    <EQUAL? .WORD ,W?DAMN ,W?SHIT ,W?FUCK>
	    <EQUAL? .WORD ,W?FUDGE ,W?PISS ,W?PEE>
	    <EQUAL? .WORD ,W?SUCK>
	    <EQUAL? .WORD ,W?POO ,W?SNOT ,W?BASTARD>>
	<PRINTC %<ASCII !\(>>
	<TELL <PICK-ONE ,OFFENDED>>
	<PRINTC %<ASCII !\)>>
	<CRLF>)>>

<GLOBAL OFFENDED
	<PLTABLE "You ought to be ashamed of yourself!"
		"Hey, save that talk for the locker room!"
		"Step outside and say that!"
		"And so's your old man!">>

<SYNONYM WITH USING BY>
<SYNONYM THROUGH THRU>
<SYNONYM IN INSIDE INTO>
<SYNONYM ON ONTO>
"<SYNONYM OUT OUTSIDE>"
<SYNONYM UNDER UNDERNEATH BENEATH>

<SYNONYM NORTH N>
<SYNONYM SOUTH S>
<SYNONYM EAST E>
<SYNONYM WEST W>
<SYNONYM DOWN D>
<SYNONYM UP U>
<SYNONYM NW NORTHW>
<SYNONYM NE NORTHE>
<SYNONYM SW SOUTHW>
<SYNONYM SE SOUTHE>

<SYNTAX $VERIFY = V-$VERIFY>

<SYNTAX \#DBG = V-DEBUG>
"<SYNTAX \#CODE = V-$CODE>"		"produces secret code from names"

<SYNTAX $BAY = V-$BAY>			"sub in bay"
"<SYNTAX $OCEAN = V-$OCEAN>"		"sub in ocean"
"<SYNTAX $DOME = V-$DOME>"		"sub in dome"
"<SYNTAX $SNARK = V-$SNARK>"		"sub in battle"

<SYNONYM ADJUST TUNE FOCUS CLEAR>
<SYNTAX ADJUST OBJECT = V-ADJUST>
<SYNTAX ADJUST OBJECT WITH OBJECT = V-ADJUST>
<SYNTAX ADJUST IN OBJECT = V-ADJUST>
<SYNTAX ADJUST UP OBJECT = V-ADJUST>

<SYNONYM AIM POINT>
<SYNTAX AIM OBJECT	AT OBJECT = V-AIM PRE-AIM>
<SYNTAX AIM OBJECT	TO OBJECT = V-AIM PRE-AIM>
<SYNTAX AIM OBJECT	OBJECT = V-AIM PRE-AIM>
<SYNTAX AIM AT OBJECT WITH OBJECT = V-SAIM PRE-SAIM>

<SYNONYM TEST ANALYSE ANALYZE CHECK ;DUST TROUBLESHOOT>
<SYNTAX TEST OBJECT = V-ANALYZE>
<SYNTAX TEST OBJECT (FIND RMUNGBIT) FOR OBJECT = V-ANALYZE>
<SYNTAX TEST OUT OBJECT = V-ANALYZE>
<SYNTAX TEST UNDER OBJECT = V-LOOK-UNDER>
<SYNTAX TEST OBJECT IN OBJECT = V-LOOK-UP>
<SYNTAX TEST OBJECT ON OBJECT = V-SANALYZE PRE-SANALYZE>

<SYNONYM ANSWER REPLY RESPOND>
<SYNTAX ANSWER = V-ANSWER>
<SYNTAX ANSWER OBJECT = V-REPLY>

<SYNTAX APPLY OBJECT (HELD MANY HAVE) TO OBJECT = V-PUT PRE-PUT>

<SYNTAX ARM OBJECT = V-ARM>

<SYNONYM ARREST CAPTURE>
<SYNTAX ARREST OBJECT (FIND PERSON) ;(MANY) = V-ARREST PRE-ARREST>
<SYNTAX ARREST OBJECT (FIND PERSON) ;(MANY) FOR OBJECT = V-ARREST PRE-ARREST>

<SYNONYM ASK QUESTION INQUIRE CONSULT>
<SYNTAX ASK OBJECT (FIND PERSON) = V-ASK PRE-ASK>
<SYNTAX ASK ABOUT OBJECT (HELD CARRIED HAVE)
	= V-ASK-CONTEXT-ABOUT PRE-ASK-CONTEXT-ABOUT>
<SYNTAX ASK FOR OBJECT (HELD CARRIED HAVE)
	= V-ASK-CONTEXT-FOR PRE-ASK-CONTEXT-FOR>
<SYNTAX ASK OBJECT (FIND PERSON)
	ABOUT OBJECT (HELD CARRIED HAVE) = V-ASK-ABOUT PRE-ASK-ABOUT>
<SYNTAX ASK OBJECT (FIND PERSON)
	FOR OBJECT (HELD CARRIED HAVE) = V-ASK-FOR PRE-ASK-FOR>

<SYNONYM ATTACK FIGHT RAPE MOLEST ASSAULT REPEL>
<SYNTAX ATTACK OBJECT (FIND PERSON) = V-ATTACK>
<SYNTAX ATTACK OFF OBJECT (FIND PERSON) = V-ATTACK>
<SYNTAX ATTACK OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	WITH OBJECT (FIND WEAPONBIT) (HELD CARRIED HAVE)
	= V-ATTACK>

<SYNTAX BRIEF = V-BRIEF>

<SYNONYM BRING FETCH>
<SYNTAX BRING OBJECT = V-BRING PRE-BRING>
<SYNTAX BRING OBJECT OBJECT = V-SBRING PRE-SBRING>
<SYNTAX BRING OBJECT TO OBJECT = V-BRING PRE-BRING>

<SYNONYM BRUSH CLEAN WIPE>
<SYNTAX BRUSH OBJECT (HELD CARRIED ON-GROUND IN-ROOM) = V-BRUSH>
"<SYNTAX BRUSH OFF OBJECT (HELD CARRIED) = V-BRUSH>"
<SYNTAX BRUSH OBJECT (HELD CARRIED ON-GROUND IN-ROOM) WITH OBJECT
	= V-BRUSH>

"<SYNONYM BURN INCINERATE IGNITE>
<SYNTAX BURN OBJECT (FIND BURNBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	WITH OBJECT (FIND FLAMEBIT) (HELD CARRIED ON-GROUND IN-ROOM HAVE)
	= V-BURN PRE-BURN>
<SYNTAX BURN DOWN OBJECT (FIND BURNBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	WITH OBJECT (FIND FLAMEBIT) (HELD CARRIED ON-GROUND IN-ROOM HAVE)
	= V-BURN PRE-BURN>"

<SYNONYM CALL ;HEY PHONE SUMMON SIGNAL CONTACT>
"<SYNTAX CALL = V-CALL-LOSE>"
<SYNTAX $CALL OBJECT = V-$CALL>
<SYNTAX CALL OBJECT = V-PHONE PRE-PHONE>
<SYNTAX CALL OBJECT BY OBJECT = V-PHONE PRE-PHONE>
"<SYNTAX CALL OBJECT FOR OBJECT = V-CALL-FOR>"
<SYNTAX CALL OBJECT ON OBJECT = V-PHONE PRE-PHONE>
<SYNTAX CALL FOR OBJECT = V-PHONE PRE-PHONE>
<SYNTAX CALL TO OBJECT = V-$CALL>
<SYNTAX CALL UP OBJECT = V-PHONE PRE-PHONE>

<SYNONYM CLIMB BOARD>
<SYNTAX CLIMB OBJECT (ON-GROUND IN-ROOM) = V-BOARD> "(FIND CLIMBBIT)"
<SYNTAX CLIMB DOWN OBJECT (ON-GROUND IN-ROOM) = V-CLIMB-DOWN>"(FIND CLIMBBIT)"
<SYNTAX CLIMB IN OBJECT (FIND VEHBIT)(ON-GROUND IN-ROOM) = V-BOARD>
<SYNTAX CLIMB ON OBJECT (FIND VEHBIT) (ON-GROUND IN-ROOM) = V-CLIMB-ON>
<SYNTAX CLIMB OUT OBJECT (FIND RMUNGBIT) (ON-GROUND IN-ROOM) = V-DISEMBARK>
<SYNTAX CLIMB THROUGH OBJECT (FIND DOORBIT) = V-THROUGH PRE-THROUGH>
<SYNTAX CLIMB UP OBJECT (ON-GROUND IN-ROOM) = V-CLIMB-UP> "(FIND CLIMBBIT)"

<SYNONYM CLOSE REPLACE RESET>
<SYNTAX CLOSE OBJECT (FIND DOORBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	= V-CLOSE>
<SYNTAX CLOSE OBJECT (FIND DOORBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	WITH OBJECT
	= V-CLOSE>

<SYNTAX COME OBJECT = V-WALK-TO ;V-COME>
<SYNTAX COME TO OBJECT = V-WALK-TO ;V-COME>
<SYNTAX COME WITH OBJECT = V-FOLLOW>
<SYNTAX COME WITH OBJECT TO OBJECT = V-COME-WITH PRE-COME-WITH>

<SYNONYM MATCH COMPARE>
<SYNTAX MATCH OBJECT (MANY) = V-COMPARE PRE-COMPARE>
<SYNTAX MATCH OBJECT (HELD CARRIED ON-GROUND IN-ROOM)
	TO OBJECT (HELD CARRIED ON-GROUND IN-ROOM) = V-COMPARE>
<SYNTAX MATCH OBJECT (HELD CARRIED ON-GROUND IN-ROOM)
	WITH OBJECT (HELD CARRIED ON-GROUND IN-ROOM) = V-COMPARE>

<SYNTAX CONFRONT OBJECT (FIND PERSON) WITH OBJECT = V-CONFRONT>

"<SYNONYM COUNT>
<SYNTAX COUNT OBJECT = V-COUNT>

<SYNONYM CROSS FORD>
<SYNTAX CROSS OBJECT = V-CROSS>"

<SYNONYM CUT INTERRUPT ;"SLICE PIERCE">
<SYNTAX CUT OBJECT ;"WITH OBJECT (FIND WEAPONBIT) (CARRIED HELD)" = V-CUT>

<SYNONYM DAMAGE DESTROY BREAK SMASH CRUSH TEAR>
<SYNTAX DAMAGE OBJECT (ON-GROUND IN-ROOM HELD CARRIED)
	= V-MUNG>
<SYNTAX DAMAGE OBJECT (ON-GROUND IN-ROOM HELD CARRIED)
	WITH OBJECT (HELD CARRIED TAKE)
	= V-MUNG>
<SYNTAX DAMAGE DOWN OBJECT (ON-GROUND IN-ROOM HELD CARRIED)
	WITH OBJECT (HELD CARRIED TAKE)
	= V-MUNG>

<SYNTAX DIAGNOSE = V-DIAGNOSE>
<SYNTAX DIAGNOSE OBJECT = V-DIAGNOSE>

"<SYNTAX DIAL OBJECT = V-PHONE>
<SYNTAX DIAL OBJECT ON OBJECT = V-PHONE>
<SYNTAX DIAL UP OBJECT = V-PHONE>"

<SYNTAX DISCUSS OBJECT = V-DISCUSS PRE-DISCUSS>
<SYNTAX DISCUSS OBJECT WITH OBJECT = V-DISCUSS PRE-DISCUSS>

<SYNONYM DIVE SUBMERGE DESCEND>
<SYNTAX DIVE = V-DIVE>
<SYNTAX DIVE OBJECT = V-CLIMB-DOWN>
<SYNTAX DIVE BY OBJECT = V-CLIMB-DOWN>
<SYNTAX DIVE TO OBJECT = V-DIVE>
<SYNTAX DIVE UNDER OBJECT (FIND RMUNGBIT) = V-DIVE>

<SYNTAX DOCK OBJECT (FIND RMUNGBIT) = V-DOCK>
<SYNTAX DOCK AT OBJECT = V-DOCK>
<SYNTAX DOCK WITH OBJECT = V-DOCK>

<SYNONYM DRINK IMBIBE SWALLOW>
<SYNTAX DRINK OBJECT ;(FIND DRINKBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	= V-DRINK>

<SYNONYM DROP LOWER>
<SYNTAX DROP OBJECT (HELD MANY HAVE) = V-DROP>
<SYNTAX DROP OBJECT (HELD MANY HAVE) DOWN ;ON OBJECT = V-PUT PRE-PUT>
<SYNTAX DROP OBJECT (HELD MANY HAVE) IN OBJECT = V-PUT PRE-PUT>
<SYNTAX DROP OBJECT (HELD MANY HAVE) ON OBJECT = V-PUT PRE-PUT>

<SYNONYM EAT CONSUME>
<SYNTAX EAT OBJECT ;(FIND FOODBIT) (HELD CARRIED ON-GROUND IN-ROOM TAKE MANY)
	= V-EAT>

<SYNONYM EMPTY DRAIN BLOW>
<SYNTAX EMPTY OBJECT (FIND CONTBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	= V-EMPTY>

<SYNTAX ENTER = V-ENTER>
<SYNTAX ENTER OBJECT = V-THROUGH PRE-THROUGH>

<SYNONYM EXAMINE DESCRIBE INSPECT SCRUTINIZE STUDY OBSERVE EXPLORE>
<SYNTAX EXAMINE OBJECT (HELD CARRIED IN-ROOM ON-GROUND ;MANY)
	= V-EXAMINE PRE-EXAMINE>
<SYNTAX EXAMINE UNDER OBJECT = V-LOOK-UNDER>

<SYNONYM EXTINGUISH DOUSE>
<SYNTAX EXTINGUISH
	OBJECT (FIND LIGHTBIT) (MANY HELD CARRIED ON-GROUND IN-ROOM TAKE HAVE)
	= V-LAMP-OFF>

<SYNONYM FILL FLOOD>
<SYNTAX FILL OBJECT (FIND CONTBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	= V-FILL PRE-FILL>
<SYNTAX FILL OBJECT (FIND CONTBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	WITH OBJECT
	= V-FILL PRE-FILL>

<SYNONYM FIND DETECT LOCATE>
<SYNTAX FIND OBJECT (;"HELD CARRIED" HAVE MANY) = V-FIND PRE-FIND>
<SYNTAX FIND OBJECT (;"HELD CARRIED" HAVE MANY) WITH OBJECT = V-FIND-WITH>
<SYNTAX FIND OUT OBJECT = V-ASK-CONTEXT-ABOUT PRE-ASK-CONTEXT-ABOUT>

<SYNTAX FIT OBJECT (HELD MANY HAVE) ON OBJECT = V-PUT PRE-PUT>
<SYNTAX FIT OBJECT (HELD MANY HAVE) TO OBJECT = V-PUT PRE-PUT>

<SYNONYM FIX REPAIR>
<SYNTAX FIX OBJECT = V-FIX>
<SYNTAX FIX OBJECT WITH OBJECT (FIND TOOLBIT) = V-FIX>

<SYNONYM FOLLOW PURSUE CHASE TRACE>
<SYNTAX FOLLOW OBJECT = V-FOLLOW>
<SYNTAX FOLLOW OBJECT (FIND PERSON) OBJECT = V-FOLLOW>	"Follow me north."
<SYNTAX FOLLOW OBJECT (FIND PERSON) TO OBJECT = V-FOLLOW> "F. me to office."

<SYNONYM HAND GIVE DONATE OFFER ;"ASSIGN" FEED>
<SYNTAX HAND OBJECT (MANY HELD HAVE ;"CARRIED ON-GROUND IN-ROOM")
	TO OBJECT (FIND PERSON) (ON-GROUND)
	= V-GIVE PRE-GIVE>
<SYNTAX HAND OBJECT (FIND PERSON) (ON-GROUND)
	OBJECT (MANY HELD HAVE)
	= V-SGIVE PRE-SGIVE>

<SYNONYM GOODBYE GOOD-BYE BYE>
<SYNTAX GOODBYE = V-GOODBYE PRE-GOODBYE>
<SYNTAX GOODBYE OBJECT = V-GOODBYE PRE-GOODBYE>

<SYNTAX HANG UP OBJECT (FIND RMUNGBIT) = V-HANGUP PRE-HANGUP>
<SYNTAX HANG OBJECT (HELD MANY HAVE) ON OBJECT = V-PUT PRE-PUT>

<SYNONYM HELLO HI GREET GREETINGS>
<SYNTAX HELLO = V-HELLO PRE-HELLO>
<SYNTAX HELLO OBJECT = V-HELLO PRE-HELLO>

<SYNTAX HELP = V-HELP>
<SYNTAX HELP OBJECT (FIND PERSON) = V-HELP>

<SYNONYM HIDE CROUCH>
"<SYNTAX HIDE = V-HIDE>
<SYNTAX HIDE BEHIND OBJECT = V-HIDE-BEHIND>"
<SYNTAX HIDE IN OBJECT = V-THROUGH PRE-THROUGH>
<SYNTAX HIDE ON OBJECT = V-THROUGH PRE-THROUGH>
<SYNTAX HIDE OBJECT (CARRIED HELD HAVE) IN OBJECT = V-PUT PRE-PUT>
<SYNTAX HIDE OBJECT (CARRIED HELD HAVE) UNDER OBJECT = V-PUT-UNDER>

"<SYNONYM I INVENT>"
<SYNTAX I = V-INVENTORY>
<SYNTAX INVENT = V-INVENTORY>

"<SYNONYM JUMP LEAP>
<SYNTAX JUMP = V-LEAP>
<SYNTAX JUMP ACROSS OBJECT = V-LEAP>
<SYNTAX JUMP IN OBJECT = V-LEAP>
<SYNTAX JUMP OVER OBJECT = V-LEAP>"

"<SYNONYM KICK BITE TAUNT>
<SYNTAX KICK OBJECT = V-KICK>"

<SYNONYM KILL STRANGLE>
<SYNTAX KILL OBJECT (FIND PERSON) (ON-GROUND IN-ROOM) = V-KILL>
<SYNTAX KILL OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	WITH OBJECT (FIND WEAPONBIT) (HELD CARRIED HAVE)
	= V-KILL>

<SYNTAX KISS OBJECT (FIND PERSON) (ON-GROUND IN-ROOM) = V-KISS>

<SYNONYM KNOCK RAP>
<SYNTAX KNOCK ON OBJECT = V-KNOCK>
<SYNTAX KNOCK AT OBJECT = V-KNOCK>
<SYNTAX KNOCK DOWN OBJECT (FIND PERSON) (ON-GROUND IN-ROOM) = V-ATTACK>

"<SYNTAX KNOW ABOUT OBJECT (HELD CARRIED HAVE) = V-WHAT>
<SYNTAX KNOW WHERE OBJECT (HELD CARRIED HAVE) = V-FIND PRE-FIND>"

<SYNTAX LAUNCH OBJECT (FIND RMUNGBIT) = V-LAUNCH>

"<SYNONYM LEAN STAND PROP>
<SYNTAX LEAN OBJECT AGAINST OBJECT = V-LEAN>
<SYNTAX LEAN OBJECT ON OBJECT = V-LEAN>
<SYNTAX LEAN OBJECT UP OBJECT = V-LEAN>"
<SYNTAX STAND = V-STAND>
<SYNTAX STAND UP OBJECT (FIND RMUNGBIT) = V-STAND>

<SYNTAX LEVEL OFF OBJECT (FIND RMUNGBIT) = V-LEVEL>
<SYNTAX LEVEL OUT OBJECT (FIND RMUNGBIT) = V-LEVEL>

<SYNONYM LEAVE ;"EMERGE" ABANDON EVACUATE DEBARK DISEMBARK EXIT>
<SYNTAX LEAVE = V-LEAVE>
<SYNTAX LEAVE OBJECT = V-LEAVE>
<SYNTAX LEAVE FROM OBJECT = V-LEAVE>

"<SYNONYM LIGHT>"
"<SYNTAX LIGHT OBJECT (FIND LIGHTBIT)
		     (HELD CARRIED ON-GROUND IN-ROOM ;TAKE ;HAVE)
	= V-LAMP-ON>"
"<SYNTAX LIGHT OBJECT (FIND LIGHTBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	WITH OBJECT (FIND FLAMEBIT) (HELD CARRIED TAKE HAVE)
	= V-BURN PRE-BURN>"

<SYNTAX LISTEN TO OBJECT = V-LISTEN>
<SYNTAX LISTEN AT OBJECT = V-LISTEN>

<SYNTAX LOCK OBJECT (ON-GROUND IN-ROOM) = V-LOCK ;PRE-UNLOCK>

<SYNONYM LOOK L STARE GAZE PEEK SEE>
<SYNTAX LOOK = V-LOOK>
<SYNTAX LOOK AROUND OBJECT (FIND RMUNGBIT) = V-LOOK>
<SYNTAX LOOK AT OBJECT (HELD CARRIED ON-GROUND IN-ROOM MANY)
	= V-EXAMINE PRE-EXAMINE>
<SYNTAX LOOK AT OBJECT (HELD CARRIED MANY) THROUGH OBJECT = V-READ PRE-READ>
<SYNTAX LOOK BEHIND OBJECT = V-LOOK-BEHIND>
<SYNTAX LOOK DOWN OBJECT (FIND RMUNGBIT) = V-LOOK-DOWN>
<SYNTAX LOOK FOR OBJECT (HELD CARRIED HAVE) = V-FIND>
<SYNTAX LOOK IN OBJECT (HELD CARRIED ON-GROUND IN-ROOM)
	= V-LOOK-INSIDE PRE-LOOK-INSIDE>
"<SYNTAX LOOK NEAR OBJECT = V-SEARCH-AROUND>"
<SYNTAX LOOK ON OBJECT (ON-GROUND IN-ROOM) = V-LOOK-ON>
<SYNTAX LOOK OUT OBJECT = V-LOOK-OUTSIDE>
<SYNTAX LOOK OVER OBJECT (HELD CARRIED ON-GROUND IN-ROOM) 
	= V-EXAMINE PRE-EXAMINE>
<SYNTAX LOOK UNDER OBJECT = V-LOOK-UNDER>
<SYNTAX LOOK UP OBJECT (FIND RMUNGBIT) = V-LOOK-UP>
<SYNTAX LOOK UP OBJECT IN OBJECT = V-LOOK-UP>
<SYNTAX LOOK THROUGH OBJECT (HELD CARRIED ON-GROUND IN-ROOM)
	= V-LOOK-INSIDE PRE-LOOK-INSIDE>

<SYNONYM MAKE BUILD PREPARE>
<SYNTAX MAKE OBJECT FROM OBJECT (ON-GROUND IN-ROOM) = V-MAKE>
<SYNTAX MAKE OBJECT ;WITH OBJECT (ON-GROUND IN-ROOM) = V-MAKE>

<SYNTAX MOVE OBJECT (ON-GROUND IN-ROOM) = V-MOVE PRE-MOVE>
<SYNTAX MOVE OBJECT (ON-GROUND IN-ROOM) OBJECT = V-MOVE-DIR PRE-MOVE-DIR>
<SYNTAX MOVE OBJECT TO OBJECT (FIND RMUNGBIT) = V-SET PRE-SET>

<SYNONYM NO NAY NEGATIVE NOPE>
<SYNTAX NO = V-NO>

<SYNTAX NOD AT OBJECT = V-HELLO PRE-HELLO>
<SYNTAX NOD TO OBJECT = V-HELLO PRE-HELLO>

<SYNTAX OPEN OBJECT (FIND DOORBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	= V-OPEN>
<SYNTAX OPEN UP OBJECT (FIND DOORBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	= V-OPEN>
"<SYNTAX OPEN OBJECT OBJECT (FIND RMUNGBIT) = V-SET PRE-SET>"
<SYNTAX OPEN OBJECT TO OBJECT (FIND RMUNGBIT) = V-SET PRE-SET>
<SYNTAX OPEN OBJECT (FIND DOORBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	WITH OBJECT (FIND TOOLBIT) (ON-GROUND IN-ROOM HELD CARRIED HAVE)
	= V-OPEN-WITH PRE-OPEN-WITH>

<SYNTAX PICK OBJECT = V-PICK>
<SYNTAX PICK OBJECT WITH OBJECT = V-PICK>
<SYNTAX PICK UP OBJECT (FIND TAKEBIT) (ON-GROUND MANY)
	= V-TAKE PRE-TAKE>

<SYNTAX PLAY OBJECT = V-PLAY>
<SYNTAX PLAY WITH OBJECT = V-PLAY>

"<SYNONYM POKE JAB BLIND>
<SYNTAX POKE OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	WITH OBJECT (FIND WEAPONBIT) (HELD CARRIED HAVE)
	= V-MUNG ;PRE-MUNG>"

"<SYNONYM POUR SPILL>
<SYNTAX POUR OBJECT (HELD CARRIED) = V-DROP>
<SYNTAX POUR OBJECT (HELD CARRIED) FROM OBJECT = V-DROP>
<SYNTAX POUR OBJECT (HELD CARRIED) IN OBJECT = V-DROP>
<SYNTAX POUR OBJECT (HELD CARRIED) ON OBJECT = V-POUR-ON PRE-POUR-ON>"

<SYNONYM PULL TUG>
<SYNTAX PULL OBJECT (ON-GROUND IN-ROOM) = V-MOVE PRE-MOVE>
<SYNTAX PULL OBJECT (ON-GROUND IN-ROOM) OBJECT = V-MOVE-DIR PRE-MOVE-DIR>
<SYNTAX PULL ON OBJECT (ON-GROUND IN-ROOM) = V-MOVE PRE-MOVE>

<SYNTAX PUMP OUT OBJECT (FIND CONTBIT) (HELD CARRIED ON-GROUND IN-ROOM)
	= V-EMPTY>

<SYNONYM PUSH PRESS>
<SYNTAX PUSH OBJECT (IN-ROOM ON-GROUND MANY) = V-PUSH>
<SYNTAX PUSH OBJECT (IN-ROOM ON-GROUND MANY) OBJECT
	= V-MOVE-DIR PRE-MOVE-DIR>
<SYNTAX PUSH OBJECT (IN-ROOM ON-GROUND MANY) ON OBJECT = V-PUSH>
<SYNTAX PUSH OBJECT (IN-ROOM ON-GROUND MANY) TO OBJECT
	= V-MOVE-DIR PRE-MOVE-DIR>
<SYNTAX PUSH OBJECT UNDER OBJECT = V-PUT-UNDER>
<SYNTAX PUSH ON OBJECT (IN-ROOM ON-GROUND MANY) = V-PUSH>

<SYNONYM PLACE PUT STUFF INSERT INSTALL MOUNT LOAD>
<SYNTAX PLACE OBJECT (HELD MANY HAVE) ON OBJECT = V-PUT PRE-PUT>
<SYNTAX PLACE OBJECT (HELD MANY HAVE) IN OBJECT(FIND CONTBIT) = V-PUT PRE-PUT>
<SYNTAX PLACE OBJECT (HELD MANY HAVE) DOWN ;ON OBJECT = V-PUT PRE-PUT>
"<SYNTAX PLACE OBJECT (HELD MANY HAVE) TO OBJECT = V-PUT PRE-PUT>"
<SYNTAX PLACE OBJECT (CARRIED HELD HAVE) UNDER OBJECT = V-PUT-UNDER>
"<SYNTAX PLACE OBJECT (CARRIED HELD HAVE) AGAINST OBJECT = V-LEAN>"
<SYNTAX PLACE DOWN OBJECT (HELD MANY HAVE) = V-DROP>
<SYNTAX PLACE ON OBJECT (ON-GROUND IN-ROOM) = V-LAMP-ON>	"PUT ON OBJ"

<SYNONYM QUIT Q>
<SYNTAX QUIT = V-QUIT>

<SYNONYM RAISE LIFT>
<SYNTAX RAISE OBJECT = V-RAISE>
<SYNTAX RAISE UP OBJECT = V-RAISE>
"<SYNTAX RAISE OBJECT OFF OBJECT = V-TAKE>"

<SYNONYM READ SKIM PERUSE>
<SYNTAX READ OBJECT (FIND READBIT) (HELD CARRIED ON-GROUND IN-ROOM TAKE)
	= V-READ PRE-READ>
<SYNTAX READ OBJECT (FIND READBIT) (HELD CARRIED ON-GROUND IN-ROOM TAKE)
	OBJECT = V-READ PRE-READ> 
<SYNTAX READ OBJECT (FIND READBIT) (HELD CARRIED ON-GROUND IN-ROOM TAKE)
	THROUGH OBJECT
	= V-READ PRE-READ> 
<SYNTAX READ ABOUT OBJECT
	IN OBJECT (FIND READBIT) (HELD CARRIED ON-GROUND IN-ROOM TAKE)
	= V-LOOK-UP>

"<SYNTAX REQUEST OBJECT (HELD CARRIED HAVE)
	= V-ASK-CONTEXT-FOR PRE-ASK-CONTEXT-FOR>"

<SYNTAX RESTART = V-RESTART>

<SYNONYM RESTORE ;CONTINUE RESUME>
<SYNTAX RESTORE = V-RESTORE>

<SYNONYM RING PEAL>
<SYNTAX RING OBJECT = V-RING>
<SYNTAX RING OBJECT WITH OBJECT = V-RING>

<SYNONYM RISE ASCEND>
<SYNTAX RISE = V-RISE>
<SYNTAX RISE OBJECT = V-CLIMB-UP>
<SYNTAX RISE BY OBJECT = V-CLIMB-UP>
<SYNTAX RISE TO OBJECT = V-RISE>

<SYNONYM RUB TOUCH FEEL SHADE PAT PET>
<SYNTAX RUB OBJECT = V-RUB>
<SYNTAX RUB OBJECT (TAKE HAVE) ON OBJECT = V-RUB-OVER PRE-RUB-OVER>
<SYNTAX RUB OBJECT (TAKE HAVE) OVER OBJECT = V-RUB-OVER PRE-RUB-OVER>
<SYNTAX RUB OBJECT WITH OBJECT (TAKE HAVE) = V-RUB>

<SYNONYM SAVE SUSPEND PAUSE>
<SYNTAX SAVE = V-SAVE>
<SYNTAX SAVE OBJECT (FIND PERSON) = V-HELP>

<SYNONYM SAY TALK SPEAK STATE EXPLAIN>
<SYNTAX SAY = V-SAY>
<SYNTAX SAY OBJECT = V-TALK-ABOUT PRE-TALK-ABOUT>
<SYNTAX SAY ABOUT OBJECT = V-TALK-ABOUT PRE-TALK-ABOUT>
<SYNTAX SAY IN OBJECT = V-SAY-INTO PRE-SAY-INTO>
<SYNTAX SAY TO OBJECT (FIND PERSON) (IN-ROOM) = V-TELL>
<SYNTAX SAY TO OBJECT (FIND PERSON) (IN-ROOM) ABOUT OBJECT
	= V-TELL-ABOUT PRE-TELL-ABOUT>
<SYNTAX SAY WITH OBJECT (FIND PERSON) (IN-ROOM) = V-TELL>

<SYNTAX SCORE = V-SCORE>

<SYNTAX SCREW IN OBJECT (HELD MANY HAVE) = V-SCREW-IN>
<SYNTAX SCREW OBJECT (HELD MANY HAVE) IN OBJECT = V-SCREW>

<SYNTAX SCRIPT = V-SCRIPT>

<SYNONYM SEARCH DIG>
<SYNTAX SEARCH OBJECT = V-SEARCH PRE-SEARCH>
"<SYNTAX SEARCH OBJECT AROUND OBJECT = V-SEARCH-GROUND-AROUND>"
<SYNTAX SEARCH OBJECT FOR OBJECT (HELD CARRIED HAVE)
	= V-SEARCH-FOR PRE-SEARCH-FOR>
"<SYNTAX SEARCH OBJECT NEAR OBJECT = V-SEARCH-GROUND-AROUND>
<SYNTAX SEARCH OBJECT UNDER OBJECT = V-SEARCH-GROUND-UNDER>"

"<SYNTAX SEARCH AROUND OBJECT = V-SEARCH-AROUND>"
<SYNTAX SEARCH FOR OBJECT (HELD CARRIED HAVE) = V-FIND>
<SYNTAX SEARCH IN OBJECT = V-SEARCH PRE-SEARCH>
"<SYNTAX SEARCH NEAR OBJECT = V-SEARCH-AROUND>"
<SYNTAX SEARCH UP OBJECT = V-SEARCH PRE-SEARCH>

<SYNTAX SEND OBJECT = V-SEND>
"<SYNTAX SEND FOR OBJECT = V-SEND-FOR>"
<SYNTAX SEND OUT OBJECT = V-SEND-OUT>
<SYNTAX SEND OBJECT OBJECT = V-SSEND PRE-SSEND>
<SYNTAX SEND OBJECT TO OBJECT = V-SEND-TO PRE-SEND-TO>

<SYNTAX SET OBJECT TO	OBJECT ;(FIND RMUNGBIT) = V-SET PRE-SET>
<SYNTAX SET OBJECT AT	OBJECT ;(FIND RMUNGBIT) = V-SET PRE-SET>
<SYNTAX SET OBJECT FOR	OBJECT ;(FIND RMUNGBIT) = V-SET PRE-SET>
<SYNTAX SET OBJECT IN	OBJECT ;(FIND RMUNGBIT) = V-SET PRE-SET>
<SYNTAX SET OBJECT (HELD MANY HAVE) ON OBJECT (FIND RMUNGBIT) = V-PUT PRE-PUT>
<SYNTAX SET OBJECT	OBJECT ;(FIND RMUNGBIT) = V-SET PRE-SET>

<SYNONYM FIRE SHOOT BLAST>
<SYNTAX FIRE OBJECT	;(FIND PERSON)		(ON-GROUND IN-ROOM)
	WITH OBJECT	(FIND WEAPONBIT)	;(HELD CARRIED HAVE)
	= V-SHOOT PRE-SHOOT>
<SYNTAX FIRE OBJECT (FIND PERSON) = V-SHOOT PRE-SHOOT>
<SYNTAX FIRE OBJECT	(FIND WEAPONBIT)	;(HELD CARRIED HAVE)
	AT OBJECT	;(FIND PERSON)		(ON-GROUND IN-ROOM)
	= V-SSHOOT PRE-SSHOOT>

<SYNTAX SHOW OBJECT (FIND PERSON) OBJECT = V-SHOW>
<SYNTAX SHOW OBJECT TO OBJECT (FIND PERSON) = V-SSHOW PRE-SSHOW>

<SYNTAX SIT ON OBJECT ;(FIND FURNITURE) (ON-GROUND IN-ROOM) = V-SIT>
<SYNTAX SIT DOWN OBJECT ;(FIND FURNITURE) (ON-GROUND IN-ROOM) = V-SIT>
<SYNTAX SIT IN OBJECT ;(FIND FURNITURE) (ON-GROUND IN-ROOM) = V-SIT>

<SYNONYM SLAP HURT INJURE HIT PUNCH KICK BITE>
<SYNTAX SLAP OBJECT (FIND PERSON) = V-SLAP>
<SYNTAX SLAP OBJECT (FIND PERSON) AROUND = V-SLAP>
<SYNTAX SLAP OBJECT (FIND PERSON) WITH OBJECT = V-SLAP>

"<SYNONYM SLEEP NAP>
<SYNTAX SLEEP OBJECT (FIND RMUNGBIT) = V-SLEEP-FOR>
<SYNTAX SLEEP FOR OBJECT = V-SLEEP-FOR>
<SYNTAX SLEEP TILL OBJECT = V-SLEEP-UNTIL>
<SYNTAX SLEEP UNTIL OBJECT = V-SLEEP-UNTIL>

<SYNTAX SLIDE OBJECT UNDER OBJECT = V-PUT-UNDER>"

<SYNONYM SMELL SNIFF>
<SYNTAX SMELL OBJECT = V-SMELL>

<SYNONYM SMILE GRIN WINK>
<SYNTAX SMILE AT OBJECT = V-SMILE>

<SYNTAX SMOKE OBJECT = V-SMOKE> "(FIND BURNBIT)"

"<SYNTAX SPACE = V-SPACE>"

<SYNONYM START ACTIVATE ENGAGE>
<SYNTAX START OBJECT = V-LAMP-ON>
<SYNTAX START OVER OBJECT (FIND RMUNGBIT) = V-RESTART>

<SYNTAX STOP OBJECT (FIND RMUNGBIT) = V-STOP>
"<SYNTAX SLOW = V-SLOW>
<SYNTAX MEDIUM = V-MEDIUM>
<SYNTAX FAST = V-FAST>"

<SYNTAX STRIKE OBJECT (FIND PERSON) (ON-GROUND IN-ROOM) = V-SLAP>
"<SYNTAX STRIKE OBJECT (ON-GROUND IN-ROOM HELD CARRIED) = V-LAMP-ON>"
<SYNTAX STRIKE OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	WITH OBJECT (FIND WEAPONBIT) (HELD CARRIED ON-GROUND IN-ROOM HAVE)
	= V-ATTACK>

<SYNONYM SUPER SUPERBRIEF>
<SYNTAX SUPER = V-SUPER-BRIEF>

<SYNTAX SURFACE = V-SURFACE>
<SYNTAX SURFACE OBJECT = V-SURFACE>

<SYNONYM SWIM BATHE WADE>
<SYNTAX SWIM = V-SWIM>
<SYNTAX SWIM IN OBJECT = V-SWIM>

"<SYNONYM SWING THRUST>
<SYNTAX SWING OBJECT (FIND WEAPONBIT) (HELD CARRIED HAVE)
	= V-SWING>
<SYNTAX SWING OBJECT (FIND WEAPONBIT) (HELD CARRIED HAVE)
	AT OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	= V-SWING>"

<SYNONYM TAKE GET ;HOLD CARRY REMOVE LEAD STEAL SEIZE GRAB>
<SYNTAX TAKE OBJECT (FIND TAKEBIT) (ON-GROUND IN-ROOM MANY)
	= V-TAKE PRE-TAKE>
<SYNTAX TAKE OBJECT (FIND TAKEBIT) (IN-ROOM CARRIED MANY)
	FROM OBJECT
	= V-TAKE PRE-TAKE>
<SYNTAX TAKE OBJECT (FIND TAKEBIT) (CARRIED IN-ROOM MANY)
	OFF OBJECT
	= V-TAKE PRE-TAKE>
<SYNTAX TAKE OBJECT (FIND TAKEBIT) (CARRIED IN-ROOM MANY)
	OUT ;OF OBJECT (FIND CONTBIT)
	= V-TAKE PRE-TAKE>
<SYNTAX TAKE OBJECT (FIND TAKEBIT) (MANY TAKE) TO OBJECT = V-TAKE-TO>
<SYNTAX TAKE OBJECT (FIND TAKEBIT) WITH OBJECT = V-TAKE-WITH>
<SYNTAX TAKE IN OBJECT (FIND VEHBIT) (ON-GROUND IN-ROOM) = V-BOARD>
<SYNTAX TAKE OFF OBJECT = V-DROP>
<SYNTAX TAKE ON OBJECT (FIND VEHBIT) (ON-GROUND IN-ROOM) = V-CLIMB-ON>
<SYNTAX TAKE OUT OBJECT (FIND RMUNGBIT) (ON-GROUND IN-ROOM) = V-DISEMBARK>
<SYNTAX TAKE RID ;OF OBJECT = V-DROP>
<SYNTAX TAKE UP OBJECT (FIND RMUNGBIT) = V-STAND>

<SYNTAX TELL OBJECT (FIND PERSON) (IN-ROOM) = V-TELL>
<SYNTAX TELL OBJECT (FIND PERSON) (IN-ROOM)
	OBJECT ;(HELD CARRIED HAVE) = V-TELL-ABOUT PRE-TELL-ABOUT>
<SYNTAX TELL ABOUT OBJECT ;(HELD CARRIED HAVE)
	= V-TALK-ABOUT PRE-TALK-ABOUT>
<SYNTAX TELL OBJECT (FIND PERSON) (IN-ROOM)
	ABOUT OBJECT ;(HELD CARRIED HAVE) = V-TELL-ABOUT PRE-TELL-ABOUT>

<SYNONYM THANKS THANK>
<SYNTAX THANKS = V-THANKS>
<SYNTAX THANKS OBJECT = V-THANKS>

<SYNONYM THROW HURL CHUCK TOSS>
<SYNTAX THROW AWAY OBJECT (HELD CARRIED HAVE) = V-DROP>
<SYNTAX THROW OBJECT (HELD CARRIED HAVE)
	AT OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	= V-THROW-AT>
<SYNTAX THROW OBJECT (HELD CARRIED HAVE)
	IN OBJECT = V-PUT PRE-PUT>
<SYNTAX THROW OBJECT (HELD CARRIED HAVE)
	ON OBJECT = V-PUT PRE-PUT>
<SYNTAX THROW OBJECT (HELD CARRIED HAVE)
	OVER OBJECT = V-PUT PRE-PUT>
<SYNTAX THROW OBJECT (HELD CARRIED HAVE)
	THROUGH OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	= V-THROW-THROUGH>

<SYNONYM TIE FASTEN SECURE ATTACH>
"<SYNTAX TIE OBJECT TO OBJECT = V-TIE PRE-TIE>"
<SYNTAX TIE OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	TO OBJECT ;(FIND FURNITURE) (ON-GROUND IN-ROOM)
	= V-TIE-TO PRE-TIE-TO>
<SYNTAX TIE UP OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	TO OBJECT ;(FIND FURNITURE) (ON-GROUND IN-ROOM)
	= V-TIE-TO PRE-TIE-TO>
<SYNTAX TIE OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	WITH OBJECT (FIND TOOLBIT) (ON-GROUND IN-ROOM HELD CARRIED HAVE)
	= V-TIE-WITH PRE-TIE-WITH>
<SYNTAX TIE UP OBJECT (FIND PERSON) (ON-GROUND IN-ROOM)
	WITH OBJECT (FIND TOOLBIT) (ON-GROUND IN-ROOM HELD CARRIED HAVE)
	= V-TIE-WITH PRE-TIE-WITH>

"<SYNONYM TIME T>
<SYNTAX TIME = V-TIME>"

<SYNONYM SWITCH FLIP SHUT SWING ;"AIM POINT">
<SYNTAX SWITCH OBJECT (HELD CARRIED ON-GROUND IN-ROOM) = V-TURN>
<SYNTAX TURN OBJECT (HELD CARRIED ON-GROUND IN-ROOM) = V-TURN>
<SYNTAX SWITCH OBJECT	ON OBJECT = V-SET PRE-SET>
<SYNTAX TURN OBJECT	ON OBJECT = V-SET PRE-SET>
<SYNTAX SWITCH OBJECT	TO OBJECT = V-SET PRE-SET>
<SYNTAX TURN OBJECT	TO OBJECT = V-SET PRE-SET>
"<SYNTAX SWITCH IN	OBJECT (HELD CARRIED ON-GROUND IN-ROOM) = V-TURN>"
<SYNTAX SWITCH OFF OBJECT(FIND LIGHTBIT) (HELD CARRIED ON-GROUND IN-ROOM MANY)
	= V-LAMP-OFF>
<SYNTAX TURN OFF OBJECT(FIND LIGHTBIT) (HELD CARRIED ON-GROUND IN-ROOM MANY)
	= V-LAMP-OFF>
<SYNTAX SWITCH OFF OBJECT(FIND LIGHTBIT) (HELD CARRIED ON-GROUND IN-ROOM MANY)
	WITH OBJECT
	= V-LAMP-OFF>
<SYNTAX TURN OFF OBJECT(FIND LIGHTBIT) (HELD CARRIED ON-GROUND IN-ROOM MANY)
	WITH OBJECT
	= V-LAMP-OFF>
<SYNTAX SWITCH ON OBJECT (FIND LIGHTBIT) (HELD CARRIED ON-GROUND IN-ROOM MANY)
	= V-LAMP-ON>
<SYNTAX TURN ON OBJECT (FIND LIGHTBIT) (HELD CARRIED ON-GROUND IN-ROOM MANY)
	= V-LAMP-ON>
<SYNTAX SWITCH ON OBJECT (FIND LIGHTBIT) (HELD CARRIED ON-GROUND IN-ROOM MANY)
	WITH OBJECT
	= V-LAMP-ON>
<SYNTAX TURN ON OBJECT (FIND LIGHTBIT) (HELD CARRIED ON-GROUND IN-ROOM MANY)
	WITH OBJECT
	= V-LAMP-ON>	"? IS MANY O.K.?"
<SYNTAX TURN TO OBJECT (HELD CARRIED ON-GROUND IN-ROOM)
	= V-TURN>

<SYNTAX UNLOCK OBJECT (ON-GROUND IN-ROOM) = V-UNLOCK>
<SYNTAX UNLOCK OBJECT (ON-GROUND IN-ROOM) WITH OBJECT = V-UNLOCK>

<SYNTAX UNSCRIPT = V-UNSCRIPT>

"<SYNTAX UNSPACE = V-UNSPACE>"

<SYNONYM UNTIE FREE UNFASTEN UNATTACH UNHOOK UNCUFF>
<SYNTAX UNTIE OBJECT (ON-GROUND IN-ROOM HELD CARRIED)
	= V-UNTIE>
<SYNTAX UNTIE OBJECT (ON-GROUND IN-ROOM HELD CARRIED)
	FROM OBJECT
	= V-UNTIE>

<SYNTAX USE OBJECT = V-USE>
<SYNTAX USE OBJECT AGAINST OBJECT = V-USE-AGAINST>

<SYNTAX VERBOSE = V-VERBOSE>

<SYNONYM RELEASE REVISION VERSION EDITION>
<SYNTAX RELEASE = V-VERSION>
<SYNTAX RELEASE OBJECT (FIND PERSON) = V-UNTIE>

<SYNONYM WAIT Z STAY MEDITATE RELAX REST>
"<SYNTAX WAIT = V-WAIT>"
<SYNTAX WAIT OBJECT (FIND RMUNGBIT) = V-WAIT-FOR>
<SYNTAX WAIT FOR OBJECT (FIND RMUNGBIT) = V-WAIT-FOR>
<SYNTAX WAIT TILL OBJECT = V-WAIT-UNTIL>
<SYNTAX WAIT UNTIL OBJECT = V-WAIT-UNTIL>

<SYNONYM WAKE AWAKE SURPRISE STARTLE>
<SYNTAX WAKE OBJECT (FIND PERSON) (ON-GROUND IN-ROOM) = V-ALARM>
<SYNTAX WAKE UP OBJECT (FIND PERSON) (ON-GROUND IN-ROOM) = V-ALARM>

<SYNONYM WALK GO RUN PROCEED STEER HEAD DRIVE>
<SYNTAX WALK  OBJECT = V-WALK>
<SYNTAX WALK  AROUND OBJECT = V-WALK-AROUND>
<SYNTAX WALK  BEHIND OBJECT = V-THROUGH PRE-THROUGH>
<SYNTAX WALK DOWN OBJECT (ON-GROUND IN-ROOM) = V-CLIMB-DOWN> "(FIND CLIMBBIT)"
<SYNTAX WALK  IN OBJECT = V-THROUGH PRE-THROUGH>
<SYNTAX WALK OBJECT OVER OBJECT = V-RUN-OVER>
<SYNTAX WALK  OUT ;OF OBJECT = V-LEAVE>
<SYNTAX WALK  TO OBJECT = V-WALK-TO>
<SYNTAX WALK  UNDER OBJECT = V-WALK-UNDER>
<SYNTAX WALK UP OBJECT (ON-GROUND IN-ROOM) = V-CLIMB-UP> "(FIND CLIMBBIT)"
<SYNTAX WALK  THROUGH OBJECT = V-THROUGH PRE-THROUGH>
<SYNTAX WALK WITH OBJECT = V-FOLLOW>

"<SYNTAX WATCH OBJECT = V-EXAMINE>"

<SYNONYM NAME IDENTIFY>
<SYNTAX NAME OBJECT (HELD CARRIED HAVE) = V-WHAT>

"<SYNTAX WHAT ABOUT OBJECT (HELD CARRIED HAVE) = V-WHAT>
<SYNTAX WHAT IN OBJECT (HELD CARRIED IN-ROOM ON-GROUND MANY)
	= V-LOOK-INSIDE PRE-LOOK-INSIDE>
<SYNTAX WHAT NEAR OBJECT = V-WHO-WAS-NEAR>
<SYNTAX WHAT ON OBJECT (HELD CARRIED IN-ROOM ON-GROUND MANY)
	= V-LOOK-INSIDE PRE-LOOK-INSIDE>

<SYNTAX WHEN = V-THANKS>
<SYNONYM WHEN DID WHY HOW>"

<SYNONYM YELL SCREAM SHOUT>
<SYNTAX YELL = V-YELL>
<SYNTAX YELL FOR OBJECT = V-YELL-FOR>
<SYNTAX YELL IN OBJECT = V-SAY-INTO PRE-SAY-INTO>
<SYNTAX YELL TO OBJECT (FIND PERSON) (IN-ROOM) = V-TELL>

<SYNONYM YES Y AYE ROGER WILCO OK OKAY 10-4 SURE YUP YEAH AFFIRM>
<SYNTAX YES = V-YES>
